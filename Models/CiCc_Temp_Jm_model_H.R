###############################
###      Bayesian Model    ####
### Following Traditional Farquhar PS model #### 
###############################
CiCc_Temp_Jm <- "model {
 
    
for (i in 1:N){
    An[i] ~ dnorm( mu.A[i] , tau )
    #mu.A[i] <- min(Ac[i], Aj[i])
    mu.A[i] <- ifelse(CiP[i] < CCrit, Ac[i], Aj[i])
### Temp Dependencies on Pars (Arrhenius Temp Functions)###
#  Arrhenius Temp function 
#                                          ( Ee (Tobs - 298))
##     Y = f(Y25, Ee, Tobs) = Y25 * exp (-----------------------)
#                                          ( 298 * R * Tobs)

gammaS[i] <- gammaS25*exp((T[i]-Tref)*EgammaS/(Kref*R*K[i]))
Rd[i] <- Rd25[geno[i]]* exp((T[i]-Tref)*ERd/(Kref*R*K[i]))
Kc[i] <- Kc25*exp((T[i]-Tref)*EKc/(Kref*R*K[i]))
Ko[i] <- Ko25*exp((T[i]-Tref)*EKo/(Kref*R*K[i]))
Vcmax[i] <- Vcmax25[geno[i]]*exp((T[i]-Tref)*EVcmax/(Kref*R*K[i]))
Jmax[i] <- Jmax25[geno[i]]*exp((T[i]-Tref)*EJmax/(Kref*R*K[i]))
gm[i] <- gm25[geno[i]]*exp((T[i]-Tref)*Egm/(Kref*R*K[i]))

#### electron transport rate described by Jmax, phiJ and thetaJ 
Ji[i] <- (Q[i] * phiJ[geno[i]]*.85)
bb[i] <- -Ji[i] -Jmax[i]
cc[i] <- Ji[i]*Jmax[i]
bac[i]<-(bb[i]^2)-(4*thetaJ[geno[i]]*cc[i])
Jm[i]<- (-bb[i]-sqrt(bac[i]))/(2*thetaJ[geno[i]])


# quadratic solution for net A if limited by light (RuBP regeneration or ETR limited) 
a2[i]<-(-1/gm[i])
b2[i]<-(((Jm[i]/4)-Rd[i])/gm[i])+(CiP[i]+2*gammaS[i])
c2[i]<-Rd[i]*(CiP[i]+2*gammaS[i])-((Jm[i]/4)*(CiP[i]-gammaS[i]))
bac2[i]<-(b2[i]^2)-(4*a2[i]*c2[i])
Aj[i]<- (-b2[i]+sqrt(bac2[i]))/(2*a2[i])


#quadratic solution for net A if limited by Rubisco
a1[i]<-(-1/gm[i])
b1[i]<-((Vcmax[i]-Rd[i])/gm[i])+(CiP[i]+(Kc[i]*((1+O[i])/Ko[i] )))
c1[i]<-Rd[i]*(CiP[i]+(Kc[i]*((1+O[i])/Ko[i] )))-Vcmax[i]*(CiP[i]-gammaS[i])
bac1[i]<-(b1[i]^2)-(4*a1[i]*c1[i])
Ac[i]<- (-b1[i]+sqrt(bac1[i]))/(2*a1[i])


}
for (k in 1:Ngeno){
    Vcmax25[k] ~ dnorm(mu.Vcmax25,tau.Vcmax25)T(0,)
    Rd25[k] ~ dnorm(mu.Rd25, tau.Rd25)
    #gammaS25[k] ~ dnorm(mu.gammaS25,tau.gammaS25)T(0,)
    gm25[k] ~ dnorm(mu.gm25,tau.gm25)T(0,)
    Jmax25[k] ~ dnorm(mu.Jmax25,tau.Jmax25)
    phiJ[k] ~ dnorm(mu.phiJ,tau.phiJ)T(0,1)
    thetaJ[k] ~ dnorm(mu.thetaJ,tau.thetaJ)T(0,1)
}
### Activation Energy Prior(s) for Arrhenius Temp Functions
#### dnorm ( mean, precision)  precision = 1/(sd^2)

EgammaS ~ dnorm(26.8, 0.5) # weakly informed prior
ERd ~ dnorm(63.9, 0.1) # weakly informed prior
EKc ~ dnorm(70.4,0.5)  # narrow prior (evolutionary constraint)
EKo ~ dnorm(36.0,0.5)  # narrow prior  (evolutionary constraint)
EVcmax ~ dnorm(65.4, 0.01) # weakly informed prior (greater variance than other E's)
EJmax ~ dnorm(46.1, 0.01) # weakly informed prior (greater variance than other E's)
Egm ~ dnorm(49.6, 0.1) # weakly informed prior

## calc Ccrit parameters
#mu.alphaC ~ dnorm(1, 0.1)T(0,)
#mu.betaC ~ dnorm(0,0.01) #Uninformative
#tau.alphaC ~ dnorm(.2, 1)
#tau.betaC ~ dnorm(1.0,1) #Uninformative
#mu.CCrit ~ dnorm(30, 0.15)T(0,)
#tau.CCrit ~ dgamma(10,200)   # this distribution has a mean SD of 4.6



### Maximum rate Carboxylation -- normal prior from Agricultural Species WULLSCHLEGER
mu.Vcmax25 ~ dnorm(90, 0.000625) ## SD of 40
tau.Vcmax25 ~ dgamma(5,5000)  # as variance distribution has mean = 34 ; SD = 9

### normal prior Dark Respiration from LR data (umol m-2 s-1)
mu.Rd25 ~ dnorm(3.97, 1)
tau.Rd25 ~ dgamma(9, 16)  # as variance distribution has mean = 1.4 ; SD = .25

### prior on gamma star (Pa) -- CO2 compensation point with photorespiration
gammaS25 ~ dnorm(3.86,10)  ### provides narrow prior based as thought highly conserver
#tau.gammaS25 ~ dgamma(50,5 )  ### mean is centered around 10 (or .3 as variance)

### prior on mesophyll conductance (umol m-2 s-1)
mu.gm25 ~ dgamma(1.85,.15)  #dnorm (2.5, 0.025)T(0,) ## wide variance due to uncertainty and possible influence of model structure
tau.gm25 ~ dgamma(4,300)  ### as variance distribution has mean = 9.5 ; SD = 2.7

### Maximum rate Electron Transport -- nromal prior from Agricultural Species WULLSCHLEGER
mu.Jmax25 ~ dnorm(171,0.000308)  # SD of approx 60
tau.Jmax25 ~ dgamma(8,25000 )   ### as variance distribution has mean = 58 ; SD = 11

### Light responce curvature parameter -- limited data in Lit. but mostly btw .4-.9
### Light responce curvature parameter -- limited data in Lit. but mostly btw .4-.9
mu.thetaJ ~ dnorm(.8, 100)T(0,1) ### SD of .316
tau.thetaJ ~ dgamma(100,5) ### as variance distribution has mean = .22 ; SD = 0.01

mu.phiJ ~ dnorm(.4, 100)T(0,1)   ### SD of .316
tau.phiJ ~ dgamma(100,5) ### as variance distribution has mean = .22 ; SD = 0.01

## Michaelis-Menten constant for CO2
Kc25 ~ dnorm(32.675,0.5)  # narrow prior (SD of 1.4) (RUBISCO evolutionary constraints)
#Michaelis-Menten constant for O2
Ko25 ~ dnorm(28612.4282, 1e-05)  # narrow prior (SD or 316) (RUBISCO evolutionary constraints)

### truncated T distribution with variance  set at 2.5 to reflect reasonable measurement error
tau ~ dt(0 , pow(2.5,-2), 1)T(0,)
}
" 

